import org.jodconverter.Deps
import org.jodconverter.Plugins

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Plugins

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'nebula.facet'
apply plugin: 'jacoco'
apply plugin: 'com.diffplug.spotless'

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Integration tests SourceSet configuration

facets {
    integTest {
        parentSourceSet = 'test'
        //includeInCheckLifecycle = false
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Dependencies

dependencies {
    implementation platform(Deps.springBootDependencies)

    compileOnly Deps.checkerQual

    testImplementation Deps.assertj
    testImplementation Deps.junitJupiterApi
    testImplementation Deps.junitJupiterParams
    testImplementation Deps.mockito

    // Required for Mockito to avoid NullPointerException
    // Mockito disclaimer:
    // You are seeing this disclaimer because Mockito is configured to create inlined mocks.
    // You can learn about inline mocks and their limitations under item #39 of the Mockito class javadoc.
    testRuntimeOnly Deps.checkerQual

    testRuntimeOnly Deps.junitJupiterEngine
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Compile

java {
    //toolchain {
    //    languageVersion = JavaLanguageVersion.of(8)
    //}
    //println("Configuring Java: " + baselineJavaVersion)
    //println("Current Java: " + JavaVersion.current())
    sourceCompatibility = baselineJavaVersion
    targetCompatibility = baselineJavaVersion
}

if (JavaVersion.current() < baselineJavaVersion) {
    throw new GradleException("This build must be run with at least Java 8.")
}

tasks.withType(JavaCompile) {
    options.encoding = sourceEncoding
    options.compilerArgs << "-Xlint:-options"
    options.compilerArgs << "-Xlint:unchecked"
    options.compilerArgs << "-Xlint:deprecation"
    options.incremental = true
    options.fork = true
    //options.debug = true
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Eclipse

eclipse {
    jdt {
        sourceCompatibility = baselineJavaVersion
        targetCompatibility = baselineJavaVersion
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Pmd

pmd {
    toolVersion = Plugins.pmdVersion
    ruleSetConfig = rootProject.resources.text.fromFile("ruleset.xml")
    ignoreFailures = true
    rulesMinimumPriority = 5
    // Don't use the default gradle rule sets
    ruleSets = []
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Checkstyle

checkstyle {
    toolVersion = Plugins.checkstyleVersion
    config = rootProject.resources.text.fromFile("checkstyle.xml")
}

// Disable checkstyle for test code
[checkstyleTest, checkstyleIntegTest]*.enabled = false

tasks.withType(Checkstyle) {
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Spotless

spotless {

    java {
        // Format code using google java format
        // Since we are running Spotless on JVM 8 (must support JVM 8), we are limited to google-java-format 1.7.
        // Remove the version when we set the minimal JVM to 11.
        googleJavaFormat('1.7')

        // Import order
        importOrderFile "$rootDir/spotless.importorder"

        // Java Source Header File
        licenseHeaderFile("$rootDir/spotless.license.java")
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Test

test {
    jvmArgs = defaultJvmArgs

    useJUnitPlatform {
        includeEngines "junit-jupiter", "junit-vintage"
    }
    failFast = true

    // Uncomment when we want to see the logs while testing
    //testLogging.showStandardStreams = true
}

integTest {
    jvmArgs = defaultJvmArgs

    useJUnitPlatform {
        includeEngines "junit-jupiter", "junit-vintage"
    }
    failFast = true

    // If you experience connection issue on windows 10, you may have to set a project property
    // pointing to a templateProfileDir where OpenGL is disabled by default. Read here to know
    // how to desable OpenGL:
    // https://wiki.documentfoundation.org/OpenGL#:~:text=LibreOffice%205.3%20and%20newer%3A,Click%20%22Apply%20Changes%20and%20Restart%22
    systemProperty "org.jodconverter.local.manager.templateProfileDir", project.findProperty("org.jodconverter.local.manager.templateProfileDir")

    // Uncomment when we want to see the logs while testing
    //testLogging.showStandardStreams = true
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Jacoco

// Configures the jacocoTestReport task
jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."

    dependsOn tasks.withType(Test)
    reports {
        html.required.set(true)
        xml.required.set(false)
        csv.required.set(false)
    }
}
check.dependsOn jacocoTestReport

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Licensing

task copyLicense(type: Copy) {
    from file("$rootProject.projectDir/LICENSE")
    into file("$projectDir/src/main/resources/META-INF/")
}

processResources {
    dependsOn copyLicense
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Artifacts

// sources jar
task sourcesJar(type: Jar, dependsOn: classes) {
    description = "Builds a source jar artifact suitable for deployment."
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    description = "Builds a javadoc jar artifact suitable for deployment."
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

jar {
    // The following options must be set here orelse the moduleName won't be define.
    afterEvaluate {
        manifest {
            def moduleName = project.hasProperty('moduleName') ? project.moduleName : project.name
            attributes(
                    "Automatic-Module-Name": project.name.replace("-", "."),
                    "Build-Jdk-Spec": baselineJavaVersion,
                    "Built-By": "JODConverter",
                    "Bundle-License": "https://github.com/jodconverter/jodconverter/wiki/LICENSE",
                    "Bundle-Vendor": "JODConverter",
                    "Bundle-DocURL": "https://github.com/jodconverter/jodconverter/wiki",
                    "Implementation-Title": moduleName,
                    "Implementation-Version": project.version,
                    "Implementation-Vendor": "JODConverter Team",
                    "Implementation-Vendor-Id": "org.jodconverter",
                    "Implementation-Url": "https://github.com/jodconverter/jodconverter",
                    "Specification-Title": moduleName,
                    "Specification-Version": project.version,
                    "Specification-Vendor": "JODConverter Team",
                    "Provider": gradle
            )
        }
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Documentation

javadoc {

    //classpath = configurations.compileClasspath + configurations.runtimeClasspath
    failOnError = false
    //source = sourceSets.main.allJava

    options.with {
        bottom = 'Copyright &#169; 2022 - present; <a href="https://github.com/jodconverter">JODConverter</a>. All rights reserved.'

        //author = false
        //breakIterator = true
        charSet = 'UTF-8'
        docEncoding = 'UTF-8'
        encoding = 'UTF-8'
        memberLevel = JavadocMemberLevel.PROTECTED
        source = baselineJavaVersion

        links(
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://api.libreoffice.org/docs/java/ref/",
                "https://commons.apache.org/proper/commons-lang/apidocs/",
                //"https://commons.apache.org/proper/commons-lang/javadocs/api-release/",
                "https://docs.spring.io/spring-boot/docs/${Deps.springBootVersion}/api/"
        )
    }
    options.addBooleanOption("Xdoclint:none")
    //options.addStringOption('Xdoclint:none', '-quiet')


    // The following options must be set here orelse the moduleName won't be define.
    afterEvaluate {
        configure(options) {
            def moduleName = project.hasProperty('moduleName') ? project.moduleName : project.name

            windowTitle = "$moduleName API Documentation"
            docTitle = "$moduleName $project.version API Documentation"
            header = "$moduleName $project.version API"
        }
    }
}